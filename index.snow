###* 
	Snow Crystals
	=============

	This script is a simple micro-framework based on [Snow](http://github.com/AndCake/snow) 
	with proper templating, caching and custom routes.

	In order to write a new route, simply add a new directory in routes and add a new
	snow file called after the action-part of the route in that directory. The URL
	will then look like that:

		http://<myserver>/<base-dir>/<snow-flake>/<snow-crystal>
	
	In the crystal file, you need to define at least a `get` function that will be called
	for all GET requests. In addition to that, you can also define a `post` function for
	POST requests or a `put` function for PUT requests and so on.

	The result of such a function should always be an array that contains the data to be
	used in the template. Example for a URL of the format 
	`http://<myserver>/<base-dir>/user/select/<id>`:

		# file is flakes/user/select.snow
		import "lib/storage"
		fn get(id)
			store = STORAGE
			<- store.get "user", id

	The template is always called like the crystal. Example (the user object returned by
	the above crystal code has a property `name`):

		<!-- flakes/user/select.tpl -->
		<h1>Welcome {{name}}!</h1>
		<p>Glad you made it here!</p>

	Caching can be activated by setting the global `CACHING` variable to anything but 
	`false`. If you set it to a number, it will interpret the number as the minutes the 
	current page should be cached. If it is anything else, it will generate an ETag cache 
	entry.

	By having a crystal return a "decorator" attribute with it's value pointing to a template
	that has a `{{body}}` tag, you can wrap the decorator around the actual crystal's 
	template.

	Example:

	Crystal "list":

		fn get <- ['decorator': 'basehtml']

	"list"'s template:

		<span class='bold'>Hello world!</span>

	Decorator `basehtml.tpl`:

		<div class='page'>
			{{body}}
		</div>

	Will result in:

		<div class='page'>
			<span class='bold'>Hello world!</span>
		</div>

	If a 404, file not found error occurs, it tries to load the flakes/error/404.snow and
	execute it's `get`/`post`/`put` function. Afterwards it will render the 404.tpl template 
	in the same directory. In case the action is not found, only the template will be rendered.

	If any other error occurs during the execution of a custom action, it will always issue
	a 500 error. Therefore it will try to load the flakes/error/500.snow and execute it's 
	`get`/`post`/`put` function. Afterwards it will render the 500.tpl template in the same 
	directory. In case the action is not found, only the template will be rendered.

	Custom URLs can also be defined by overriding the main/home crystal, in case the URL 
	does not need to be multi-parted (example: `http://<server>/<pathname>`). The 
	`pathname` segment of the URL will be transmitted to the main/home crystal as the first
	parameter. Alternatively, for multi-part URLs (example: `http://<server>/<pathname1>/<pathname2>...`)
	you can override the error/404 crystal and route to the respective actual crystal.

	All settings are stored on a file called `settings.json`, which is read into the global
	variable `CONFIG`. Any properties you write there can be obtained from the `CONFIG` object.
*###
ini_set "xdebug.max_nesting_level", 900
require_once "snowcompiler.php"

fn import(file)
	if not file_exists("{file}.php") or filemtime("{file}.php") < filemtime("{file}.snow")
		if file_exists "{file}.snow"
			snow = new SnowCompiler file_get_contents file+".snow"
			code = snow.compile()
			file_put_contents "{file}.php", "<?php\n{code}\n?>"
		else
			throw new Exception "Unable to find file {file}"
	
	include_once "{file}.php"

CONFIG = "settings.json"->file_get_contents()->json_decode()
route = _SERVER['REQUEST_URI']->replace(dirname(_SERVER['SCRIPT_NAME']), '')

# caching implementation (uses Last-Modified and ETags and server-side cached files)
CACHING = false
cacheName = "cache/" + md5("{route}-{_GET->json_encode()}")
headers = getallheaders()
if headers['If-None-Match']? and "cache/{headers['If-None-Match']}"->file_exists() or 
   headers['If-Modified-Since']? and headers['If-Modified-Since']->strtotime() >= cacheName->filemtime() and cacheName->file_exists()
	header "HTTP/1.1 304 Not Modified"
	do die

if cacheName->file_exists()
	content = cacheName->file_get_contents()
	[headers,] = explode("\r\n\r\n", content)
	headerList = headers->json_decode(true)
	if headerList['Expires']->strtotime() > time()
		for entry, key in headerList
			header "{key}: {entry}"
		die content->replace(headers+"\r\n\r\n", '')
	else
		unlink cacheName

list = explode '/', route
module = action = ''
start = 1
if list->count() > 2
	start = 3
	[, module, action] = list
if not module
	module = "main"
if not action
	action = "home"

params = list->array_slice start

header 'Server: '
header 'X-Powered-By: '

try
	import "flakes/{module}/{action}"
	type = _SERVER["REQUEST_METHOD"]->strtolower()
	data = type->call_user_func_array(params)
	data['basedir'] = dirname(_SERVER['SCRIPT_NAME'])
	type = if type is 'get' then '' else '-' + type
	if "flakes/{module}/{action % type}.tpl"->file_exists()
		result = template "flakes/{module}/{action % type}", data
	else
		result = template "flakes/{module}/{action}", data

	if data['decorator']??
		result = template "flakes/{module}/{data['decorator']}", ['body': result, 'basedir': dirname(_SERVER['SCRIPT_NAME'])]
catch ex
	page = if "Unable to find file"->in(ex.getMessage()) then "404" else "500"
	result = []
	try 
		import "flakes/error/{page}"
		result = type->call_user_func_array(params)
	catch e
		# ignore
	CACHING = false
	header "HTTP/1.1 {page} Problem"
	result = template "flakes/error/{page}", ['result': result, 'exception': ['message': ex.getMessage(), 'trace': ex.getTraceAsString()]]

if CACHING isnt false
	# if the developer chose to set a caching number
	if CACHING isa number
		cache = CACHING * 60
		# use the Last-Modified header and Expires...
		headers = [
			"Last-Modified": gmdate('D, d M Y H:i:s') + " GMT", 
			"Expires": gmdate('D, d M Y H:i:s', time() + cache) + " GMT",
			"Cache-Control": "max-age={cache}, must-revalidate"
		]
	else
		# else use the ETag header based on content
		cacheName = cacheName + "-{result->md5()}"
		headers = ["ETag": '"' + basename(cacheName) + '"']

	if not 'cache'->file_exists() then mkdir 'cache'
	for entry, key in headers
		header "{key}: {entry}"
	header "Content-Length: {result->strlen()}"
	file_put_contents cacheName, "{headers->json_encode()}\r\n\r\n{result}"

echo result
